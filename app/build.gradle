plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "com.github.ben-manes.versions"
    id "com.getkeepsafe.dexcount"
    id "com.vanniktech.android.junit.jacoco"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "com.google.firebase.firebase-perf"
}

ext {
    secrets = [
            firebaseCloudMessagingServerKey: keyProperty("FIREBASE_CLOUD_MESSAGING_SERVER_KEY")
    ]
}

android {
    namespace "com.netguru.babyguard"
    compileSdk = 34

    defaultConfig {
        applicationId "com.netguru.babyguard"
        minSdk = 21
        targetSdk = 34
        versionCode isBitrise ? Integer.parseInt(bitrise.io.buildNumber) : 1
        versionName '0.5.0'
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-instant-run.pro', 'proguard-rules-test.pro'
        }
        releasePreprod {
            signingConfig signingConfigs.debug
            versionNameSuffix "-PREPROD"
            applicationIdSuffix ".preprod"
            debuggable true
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules-release-preprod.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules-release.pro'
        }
    }

    sourceSets {
        debug { java.srcDirs += 'src/debugTools/kotlin' }
        releasePreprod { java.srcDirs += 'src/debugTools/kotlin' }
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes.configureEach {
        buildConfigField 'String', 'FIREBASE_CLOUD_MESSAGING_SERVER_KEY', "\"$secrets.firebaseCloudMessagingServerKey\""
    }

    testOptions {
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }

    packaging {
        resources {
            excludes += 'META-INF/services/javax.annotation.processing.Processor'
            excludes += 'META-INF/LICENSE.txt'
            excludes += 'META-INF/NOTICE.txt'
        }
    }

    sourceSets.all {
        it.java.srcDir "src/$it.name/kotlin"
    }

    junitJacoco {
        jacocoVersion = '0.8.11'
        excludes = ['android/databinding/**/*.class',
                    '**/android/databinding/*Binding.class',
                    '**/BR.*',
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*$ViewInjector*.*',
                    '**/*$ViewBinder*.*',
                    '**/*_MembersInjector.class',
                    '**/Dagger*Component.class',
                    '**/Dagger*Component$Builder.class',
                    '**/*Module_*Factory.class',
                    '**/AutoValue_*.*',
                    '**/*JavascriptBridge.class',
                    '**/Lambda$*.class',
                    '**/Lambda.class',
                    '**/*Lambda.class',
                    '**/*Lambda*.class']
    }

    lint {
        abortOnError = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

kotlin {
    jvmToolchain(17)
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "17"
    }
}

kapt {
    correctErrorTypes = true
}

configurations {
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

dependencies {
    //Kotlin
    implementation libs.kotlin
    implementation libs.constraintLayout

    //Navigation Components
    implementation libs.navigationFragment
    implementation libs.navigationUiKtx

    //Ktx
    implementation ktx.core
    implementation ktx.fragment
    implementation ktx.liveData

    //Anko Commons
    implementation libs.ankoCommons

    //Live Data
    implementation libs.lifecycleExtensions
    kapt proc.lifecycleExtensions

    //Room
    implementation libs.room
    implementation libs.roomRx
    kapt proc.room

    // Support
    implementation libs.appCompat
    implementation libs.design

    // Dagger
    implementation libs.dagger
    implementation libs.daggerAndroid
    implementation libs.daggerAndroidSupport
    kapt proc.dagger
    kapt proc.daggerAndroid
    compileOnly proc.javaxAnnotation

    //RxJava
    implementation libs.rxJava2
    implementation(libs.rxKotlin2) {
        exclude module: 'rxjava'
        exclude module: 'kotlin-stdlib'
    }
    implementation(libs.rxAndroid2) {
        exclude module: 'rxjava'
    }

    // Timber
    implementation libs.timber

    // LeakCanary
    debugImplementation libs.leakCanary

    //Glide
    implementation libs.glide
    kapt proc.glide

    //ThreeTen
    implementation libs.threeTen

    //Gson
    implementation libs.gson

    //EasyImage
    implementation libs.easyImage

    //Tensor Flow
    implementation libs.tensorFlow

    //Java Web Socket
    implementation libs.javaWebSocket

    //WebRtc
    implementation libs.googleWebRtc

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:33.1.0')
    implementation libs.firebaseStorage

    //Firebase Database
    implementation libs.firebaseDatabase

    implementation libs.firebaseMessaging

    implementation libs.okHttp

    //Firebase Analytics
    implementation libs.firebaseAnalytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation libs.firebasePerformance

    implementation libs.deviceNames

    //Unit testing
    testImplementation test.junit
    testImplementation test.mockito
    testImplementation test.mockitoKotlin
    testImplementation test.lifecycleExtensions

    //Instrumented tests
    androidTestImplementation test.supportAnnotations
    androidTestImplementation test.runner
    androidTestImplementation test.rules
    androidTestImplementation test.espresso
    androidTestImplementation test.room
    androidTestImplementation test.lifecycleExtensions
}
