static def varToCamelCase(String envVarName) {
    envVarName.split('_').collect { it -> it.toLowerCase().capitalize() }.join()
}

ext {
    if (isBitrise) {
        keyProperty = { var ->
            prop = System.getenv(var)
            if (prop == null || prop.isEmpty()) {
                if (bitrise.cli.isPr) {
                    prop = "undefined"
                }
                else {
                    throw new MissingPropertyException("Missing environment variable $var")
                }
            }
            prop
        }
    } else {
        keyConfigPath = "${projectDir.path}/secret.properties"
        File keyConfigFile = file(keyConfigPath)
        keyProps = new Properties();
        final String placeholderValue = "undefined"

        if (keyConfigFile.exists()) {
            keyProps.load(keyConfigFile.newInputStream())
        } else {
            logger.lifecycle("No secret.properties found at $keyConfigPath. Using placeholder secret values. Copy secret.properties.sample to secret.properties and provide real values when available.")
        }

        keyProperty = { var ->
            propNameCase = varToCamelCase(var)

            if (!keyConfigFile.exists()) {
                return placeholderValue
            }

            prop = keyProps[propNameCase]
            if (prop == null || prop.toString().isEmpty())
                throw new MissingPropertyException("Missing property $propNameCase")
            prop
        }
    }
}
